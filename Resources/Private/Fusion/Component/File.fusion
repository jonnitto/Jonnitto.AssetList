prototype(Jonnitto.AssetList:Component.File) < prototype(Neos.Fusion:Component) {
    asset = ${null}
    forceDownload = ${null}
    thumbnail = ${null}
    namespace = ${null}
    target = ${Configuration.setting('Jonnitto.AssetList.externalTarget')}
    maximumWidth = ${Configuration.setting('Jonnitto.AssetList.thumbnail.maximumWidth')}
    downloadFileName = ${null}
    type = ${this.thumbnail ? 'thumbnail' : 'text'}

    @if.set = ${this.asset}

    renderer = Neos.Fusion:Tag {
        tagName = 'a'
        attributes {
            href = Neos.Fusion:ResourceUri {
                @if.islive = ${Carbon.Version.lowerThanNine() ? !node.context.inBackend : !renderingMode.isEdit}
                resource = ${props.asset.resource}
            }
            download = ${Type.isString(props.downloadFileName) ? props.downloadFileName : !!(props.forceDownload && this.href)}
            target = ${props.target}
            class = ${BEM.string(props.namespace, 'link', [props.type, props.asset.resource.fileextension])}
        }
        content = afx`
            <Jonnitto.AssetList:Component.FileName {...props} />
        `
    }
}
