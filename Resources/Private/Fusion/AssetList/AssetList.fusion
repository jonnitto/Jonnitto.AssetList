prototype(Jonnitto.AssetList:AssetList) < prototype(Neos.Fusion:Tag) {
    assets = ${q(node).property('assets')}
    editable = true
    forceDownload = ${q(node).property('forceDownload')}
    thumbnail = ${q(node).property('thumbnail')}
    namespace = 'assets-list'

    @context {
        assets = ${this.assets}
        editable = ${this.editable}
        forceDownload = ${this.forceDownload}
        thumbnail = ${this.thumbnail}
        namespace = ${this.namespace}
    }

    attributes.class = Neos.Fusion:RawArray {
        nodeType = 'jonnitto-assetlist'
        container = ${Configuration.setting('Jonnitto.AssetList.container')}
        list = ${namespace}
        assets = ${assets ? namespace + '--count-' + Array.length(assets): false}
        thumbnail = ${namespace + (thumbnail ? '--thumbnail' : '--text')}
    }

    content = Neos.Fusion:Case {
        hasAssets {
            condition = ${assets ? true : false}
            renderer = Jonnitto.AssetList:Collection
        }
        default {
            condition = ${node.context.currentRenderingMode.edit}
            renderer = Neos.Fusion:Tag {
                attributes.style = 'background:#f0ad4e;color:#fff;box-shadow:0 1px 10px rgba(0,0,0,0.1);padding:5% 12px;margin:30px auto;text-align:center;text-decoration:none;outline:none;font-size:20px;'
                content = ${Translation.translate('content.emptyAssetList', null, [], 'NodeTypes/AssetList', 'Jonnitto.AssetList')}
            }
        }
    }

    @if.hasAssetsOrBackend = ${node.context.inBackend || this.assets ? true : false}
    @process.contentElementWrapping {
        expression = Neos.Neos:ContentElementWrapping
        @if.editable = ${editable}
        @position = 'end 999999999'
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
