prototype(Jonnitto.AssetList:FileNameThumbnail) < prototype(Fusion:Template) {
	templatePath = 'resource://Jonnitto.AssetList/Private/Templates/AssetList.html'
	sectionName = 'FileNameThumbnail'

	headline = Fusion:Tag {
		tagName = ${Configuration.setting('Jonnitto.AssetList.thumbnail.headlineTag')}
		attributes.class = Fusion:RawArray {
			text = ${namespace + '--headline'}
		}
		content = Jonnitto.AssetList:Title
	}

	thumbnail = Fusion:Tag {
		tagName = 'figure'
		attributes.class = ${namespace + '--thumbnail'}
		content = Jonnitto.AssetList:Image
	}

	action = Fusion:Tag {
		tagName = 'div'
		attributes.class = ${namespace + '--action'}
		content = Fusion:Array {
			download = ${Translation.translate(forceDownload ? 'file.download' : 'file.open', null, [String.toUpperCase(asset.resource.fileextension)], null, 'Jonnitto.AssetList')}
			caption = ${' ' + asset.caption + ' '}
			info = Fusion:Tag {
				tagName = 'small'
				attributes.class = ${namespace + '--filesize'}
				content = Jonnitto.AssetList:Filesize
			}
		}
	}

	@if.hasAsset = ${asset}
}
