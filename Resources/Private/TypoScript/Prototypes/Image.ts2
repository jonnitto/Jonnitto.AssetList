prototype(Jonnitto.AssetList:Image) < prototype(Fusion:Tag) {
	asset = ${asset}
	@if.hasAsset = ${!!asset}

	title = false
	alternativeText = Jonnitto.AssetList:Title

	maximumWidth = ${Configuration.setting('Jonnitto.AssetList.thumbnail.width')}
	maximumHeight = false
	width = false
	height = false
	allowCropping = false
	allowUpScaling = false

	@context {
		asset = ${this.asset}
		maximumWidth = ${this.maximumWidth}
		maximumHeight = ${this.maximumHeight}
		width = ${this.width}
		height = ${this.height}
		allowCropping = ${this.allowCropping}
		allowUpScaling = ${this.allowUpScaling}
		title = ${this.title}
		alternativeText = ${this.alternativeText}
	}

	prototype(ImageUri) {
		multiplicator = 1
		asset = ${asset}
		allowCropping = ${allowCropping}
		allowUpScaling = ${allowUpScaling}
		maximumWidth = ${width ? false : maximumWidth * this.multiplicator}
		maximumHeight = ${height ? false : maximumHeight * this.multiplicator}
		width = ${width * this.multiplicator}
		height = ${height * this.multiplicator}
	}

	tagName = 'img'

	attributes {
		class = ${namespace + '--img'}
		title = ${title}
		alt = ${alternativeText}
		src = ImageUri
		srcset = Fusion:Array {
			1 = ImageUri {
				@process.addResolution = ${value + ' 1x'}
			}
			2 = ImageUri {
				multiplicator = 2
				@process.addResolution = ${',' + value + ' 2x'}
			}
		}
	}
}
